import de.richsource.gradle.plugins.gwt.Style

apply plugin: "jetty"
apply plugin: "java"

gwt {
    gwtVersion = '2.8.0' // Should match the gwt version used for building the gwt backend
    maxHeapSize = "2G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize = "1G"
    logLevel = 'ERROR'

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'net.mostlyoriginal.game.GdxDefinition'
    devModules 'net.mostlyoriginal.game.GdxDefinitionSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true
        disableCastChecking = true
        optimize = 9
        style = Style.OBF
    }
}

task draftRun(type: JettyRunWar) {
    dependsOn draftWar
    dependsOn.remove('war')
    webApp = draftWar.archivePath
    daemon = true
}

task superDev(type: de.richsource.gradle.plugins.gwt.GwtSuperDev) {
    dependsOn draftRun
    doFirst {
        gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
        }
        copy {
            from "webapp"
            into "build/dist"
        }
        copy {
            from "war"
            into "build/dist"
        }
    }
}

draftWar {
    from "war"
}

task addSource << {
    sourceSets.main.compileClasspath += files(project(':components').sourceSets.main.allJava.srcDirs)
    sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)


sourceCompatibility = 1.7
sourceSets {
    main {
        java {
            srcDir 'src/'
            exclude '**/emu/**'
        }
    }
}

eclipse.project {
    name = appName + "-html"
}
